// Frequncy: 	DC output if 0
//				AC output @Frequency if >0
// Duty Cycle:  if frequency is 0, 		outputs BAT_POS if >50%
//										outputs BAT_NEG if <-50%
//										OFF otherwise
//				if frequency >0, 		acts as regular duty cycle

when (Current State) {
	is (Idle) {
		when (Gear.Lever) {
			is (Up) {
				if (Gear.Estimate neq Gear Enumeration.Sixth and Gear.Estimate neq Gear Enumeration.Neutral) {
					Shifter Pos.Frequency = 10000;
					
					Shifter Pos.Duty Cycle = UpShift Duty Cycle;
					Shifter Neg.Frequency = 0;
					Shifter Neg.Duty Cycle = -100;
					State Cycles = Num Upshift Cycles;
					Current State = Current State.Shift Up;
					
					Custom IgCut.Start Cut(Custom IgCut.Regular Cut);
				}
			}
			is (Down) {
				if (Gear.Estimate.AsInteger() >= Gear Enumeration.Second.AsInteger()) {
					Shifter Pos.Frequency = 0;
					Shifter Pos.Duty Cycle = -100;
					Shifter Neg.Frequency = 10000;
					Shifter Neg.Duty Cycle = DnShift Duty Cycle;
					State Cycles = Num Dnshift Cycles;
					Current State = Current State.Shift Dn;
				}
			}
			is (Idle) {
				if (Engine.Speed > Preload Up Activation and Gear.Value.AsInteger() >= Gear.Value.Second.AsInteger()) {
					Shifter Pos.Frequency = 10000;
					Shifter Pos.Duty Cycle = Preload Up Duty Cycle;
					Shifter Neg.Frequency = 0;
					Shifter Neg.Duty Cycle = -100;
					Current State = Current State.Preload Up;
				} else if (Brake.Front Value > Preload Dn Activation and Gear.Value.AsInteger() > Gear.Value.Second.AsInteger()) {
					Shifter Pos.Frequency = 0;
					Shifter Pos.Duty Cycle = -100;
					Shifter Neg.Frequency = 10000;
					Shifter Neg.Duty Cycle = Preload Dn Duty Cycle;
					Current State = Current State.Preload Dn;
				}
			}
			is(Lockout or Not in Use or Hold) {
			
			}
		}
	}
	is (Preload Up) {
		if (Gear.Lever eq Gear.Lever.Up) {
			Shifter Pos.Frequency = 10000;
			Shifter Pos.Duty Cycle = UpShift Duty Cycle;
			Shifter Neg.Frequency = 0;
			Shifter Neg.Duty Cycle = -100;
			State Cycles = Num Preload Cycles;
			Current State = Current State.Shift Up;
		} else if (Engine.Speed < Preload Up Activation) {
			Shifter Pos.Frequency = 0;
			Shifter Pos.Duty Cycle = 0;
			Shifter Neg.Frequency = 0;
			Shifter Neg.Duty Cycle = 0;
			Current State = Current State.Idle;
		} else if (Gear.Lever eq Gear.Lever.Down) {
			Shifter Pos.Frequency = 0;
			Shifter Pos.Duty Cycle = -100;
			Shifter Neg.Frequency = 10000;
			Shifter Neg.Duty Cycle = DnShift Duty Cycle;
			State Cycles = Num Preload Cycles + Num Neg Preload Comp;
			Current State = Current State.Shift Dn;
		}
	}
	is (Preload Dn) {
		if (Brake.Front Value < Preload Dn Activation) {
			Shifter Pos.Frequency = 0;
			Shifter Pos.Duty Cycle = 0;
			Shifter Neg.Frequency = 0;
			Shifter Neg.Duty Cycle = 0;
			Current State = Current State.Idle;
		} else if (Gear.Lever eq Gear.Lever.Down) {
			Shifter Pos.Frequency = 0;
			Shifter Pos.Duty Cycle = -100;
			Shifter Neg.Frequency = 10000;
			Shifter Neg.Duty Cycle = DnShift Duty Cycle;
			State Cycles = Num Preload Cycles;
			Current State = Current State.Shift Dn;
		} else  if (Gear.Lever eq Gear.Lever.Up) {
			Shifter Pos.Frequency = 10000;
			Shifter Pos.Duty Cycle = UpShift Duty Cycle;
			Shifter Neg.Frequency = 0;
			Shifter Neg.Duty Cycle = -100;
			State Cycles = Num Preload Cycles + Num Neg Preload Comp;
			Current State = Current State.Shift Up;
		}
	}
	is (Shift Up) {
		if (State Cycles neq 0) {
			State Cycles = State Cycles - 1;
		} else {
			Shifter Pos.Frequency = 0;
			Shifter Pos.Duty Cycle = -100;
			Shifter Neg.Frequency = 10000;
			Shifter Neg.Duty Cycle = Return Duty Cycle;
			State Cycles = Num Return Cycles;
			Current State = Current State.Return Dn;
		}
	}
	is (Shift Dn) {
		if (State Cycles neq 0) {
			State Cycles = State Cycles - 1;
		} else {
			Shifter Pos.Frequency = 10000;
			Shifter Pos.Duty Cycle = Return Duty Cycle;
			Shifter Neg.Frequency = 0;
			Shifter Neg.Duty Cycle = -100;
			State Cycles = Num Return Cycles;
			Current State = Current State.Return up;		
		}
	}
	is (Return Dn) {
		if (State Cycles neq 0) {
			State Cycles = State Cycles - 1;
		} else {
			Shifter Pos.Frequency = 0;
			Shifter Pos.Duty Cycle = 0;
			Shifter Neg.Frequency = 0;
			Shifter Neg.Duty Cycle = 0;
			Current State = Current State.Idle;
		}
	}
	is (Return Up) {
		if (State Cycles neq 0) {
			State Cycles = State Cycles - 1;
		} else {
			Shifter Pos.Frequency = 0;
			Shifter Pos.Duty Cycle = 0;
			Shifter Neg.Frequency = 0;
			Shifter Neg.Duty Cycle = 0;
			Current State = Current State.Idle;
		}
	}
}
