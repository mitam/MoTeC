local testmode = Test eq Test.Enabled;

if (testmode)
{
	State = State.Test;
}
else
{
	when (State)
	{
		is (Enabled)
		{
			if ((Inlet.Manifold.Pressure < (Activate.Inlet Manifold Pressure Minimum - Activate.Inlet Manifold Pressure Hysteresis)) or
				(Inlet.Manifold.Pressure > (Activate.Inlet Manifold Pressure Maximum + Activate.Inlet Manifold Pressure Hysteresis)) or
				(Race Time < Activate.Race Time)
			)
			{
				State = State.Disabled;
			}
		}
		is (Disabled)
		{
			if ((Inlet.Manifold.Pressure > Activate.Inlet Manifold Pressure Minimum) and
				(Inlet.Manifold.Pressure < Activate.Inlet Manifold Pressure Maximum) and
				(Race Time >= Activate.Race Time)
			)
			{
				State = State.Enabled;
			}
		}
		is (Test)
		{
			State = State.Disabled;
		}
	}
}

local dc = 0.0;

when (State)
{
	is (Enabled)
	{
		dc = Duty Cycle;
	}
	is (Disabled)
	{
		dc = 0.0;
	}
	is (Test)
	{
		dc = Test.Duty Cycle;
	}
}

if (Polarity eq Polarity.Inverting)
{
	dc = 1.0 - dc;
}

Output.Duty Cycle = dc;
